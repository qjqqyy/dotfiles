#!/usr/bin/env perl
use strict;
use warnings;
no warnings 'uninitialized';

use IO::Select;
use IPC::Open2;
#use Data::Dumper;

# compute number of screens
my $numscreens = do {
    my @a = qx(xrandr | grep ' connected');
    scalar @a
};

$|=1;
my $dir = ($0 =~ s|[^/]*$||r);
open (my $wsp, '-|', "${dir}i3_workspaces.pl")          or die "can't open pipe to i3_workspaces: $!";
open (my $conky, '-|', 'conky', '-c', "${dir}conkyrc")  or die "can't open pipe to conky: $!";
open (my $xtitle, '-|', "${dir}xtitle", '-s', '-t91')   or die "can't open pipe to xtitle: $!";
open2 (my $lbarout, my $lemonbar, 'lemonbar', '-a', '21', '-f-sgi-screen-medium-r-normal-*-14-*-*-*-*-*-*-*', '-f-wenquanyi-wenquanyi bitmap song-medium-r-normal-*-14-*-*-*-*-*-*-*', '-f-misc-icons-medium-r-normal-*-14-*-*-*-*-*-*-*', '-B#100013', '-F#ccadcc') or die "can't open lemonbar: $!";
$lemonbar->autoflush;
close(STDIN);

$0 = "lemonstatus: master";
my $select = IO::Select->new($wsp, $xtitle, $conky, $lbarout);
#my $select = IO::Select->new($wsp, $xtitle, $lbarout);
my ($status, $title, $workspace);
while (my @ready = $select->can_read()) {
    for my $fh (@ready) {
        my $buf;
        unless (sysread $fh, $buf, 1024) {
            if (!eof $fh) {
                die $!;
            #    $select->remove($fh);
            #    close $fh;
            }
            exit;
        }
        $buf = (split /\n/,$buf)[-1];
        chomp $buf;
        if ($fh == $conky) {
            $status = $buf;
        } elsif ($fh == $xtitle) {
            $title = ($buf =~ s/%/%%{}/gr);
        } elsif ($fh == $wsp) {
            $workspace = $buf;
        } elsif ($fh == $lbarout) {
            system('i3-msg', 'workspace', $buf);
        } else {
            print STDERR "something from unknown fh: $buf\n";
        }
        #print $lemonbar "%{l B#4c114e F#ccadcc}  $workspace%{B- F#b101a9}  %{F#e5cece B-}$title %{r} $status%{B-}\n";
        print $lemonbar "%{S$_}%{l B#4c114e F#ccadcc}  $workspace%{B- F#b101a9}  %{F#e5cece B-}$title %{r} $status%{B-}" for (0..$numscreens-1);
        print $lemonbar "\n";
#        print "$workspace\n$title\n$status\n";
    }
}
