# zbell (terminal bell after long command)
zmodload zsh/datetime || return
autoload -Uz add-zsh-hook || return
zbell_duration=15
zbell_ignore=($EDITOR $PAGER less weechat ncmpcpp alsamixer top htop)
zbell_timestamp=$EPOCHSECONDS

# right before we begin to execute something, store the time it started at
zbell_begin() {
  zbell_timestamp=$EPOCHSECONDS
  zbell_lastcmd=$1
}
# when it finishes, if it's been running longer than $zbell_duration,
# and we dont have an ignored command in the line, then print a bell.
zbell_end() {
  ran_long=$(( $EPOCHSECONDS - $zbell_timestamp >= $zbell_duration ))

  has_ignored_cmd=0
  for cmd in ${(s:;:)zbell_lastcmd//|/;}; do
    words=(${(z)cmd})
    util=${words[1]}
    if (( ${zbell_ignore[(i)$util]} <= ${#zbell_ignore} )); then
      has_ignored_cmd=1
      break
    fi
  done

  if (( ! $has_ignored_cmd )) && (( ran_long )); then
    print -n "\a"
  fi
}

zbellinit() {
  add-zsh-hook preexec zbell_begin
  add-zsh-hook precmd zbell_end
}

zbellinit "$@"
